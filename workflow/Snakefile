import os
import json
import pandas as pd

# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,
# in particular regarding the standardized folder structure mentioned there.


# load configuration
# -----------------------------------------------------
configfile: "config/config.yml"

BASE = os.path.abspath(config["base_dir"]) # Base directory for ALL runs

runs_df = pd.read_csv(config["runs_table"], sep="\t")

RUNS = runs_df["run_id"].tolist()

ORGAN_MAPPING_PATH = config["organ_mapping_json"]
with open(ORGAN_MAPPING_PATH) as fh:
    organ_mapping = json.load(fh)
    ALL_ORGANS = list(organ_mapping.keys())

RUN_PARAMS = {
    row.run_id: {
        "VCF"     : row.input_vcf,
        "ORGANS"  : (
            ALL_ORGANS if row.organs.strip().lower() == "all"
            else [o.strip() for o in row.organs.split(";")]
        ),
    }
    for _, row in runs_df.iterrows()
}

# load rules
# -----------------------------------------------------
include: "rules/extract_features.smk"
include: "rules/predict.smk"


# optional messages, log and error handling
# -----------------------------------------------------
onstart:
    print("\n--- Analysis started ---\n")


onsuccess:
    print("\n--- Workflow finished! ---\n")


onerror:
    print("\n--- An error occurred! ---\n")


# target rules
# -----------------------------------------------------
rule all:
    input:
        [
            os.path.join(
                BASE,
                run,
                f"predictions/TLand_scores.{organ}.tsv.gz"
            )
            for run in RUNS
            for organ in RUN_PARAMS[run]["ORGANS"]
        ]
    default_target: True
